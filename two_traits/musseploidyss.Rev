#########################################################################################################
#
# Musse Ploidy and Sexual systems
# authors: Rosana Zenil-Ferguson
# last modified: 06/27/2024
#########################################################################################################
#######################
# Reading in the Data #
#######################
# 0 = Diploid Hermaphroditic
# 1 = Poly polyploid
# 2 = Wind diploid
# 3 = Wind polyploid

setOption("useScaling","true")
NUM_STATES = 4
### Read in the data

#observed_phylogeny <- readTrees("/mnt/scratch/nfs_fs02/roszenil/thal/thalmatchphy.tre")[1]
observed_phylogeny<- readTrees("basicdata/thalmatchphy.tre")[1]
#data <- readCharacterDataDelimited("/mnt/scratch/nfs_fs02/roszenil/thal/ploidyss.tsv",stateLabels=4,type="NaturalNumbers",delimiter="\t",headers=FALSE)
data <- readCharacterDataDelimited("basicdata/ploidyss.tsv",stateLabels=4,type="NaturalNumbers",delimiter="\t",headers=FALSE)
# Get some useful variables from the data. We need these later on.
taxa <- observed_phylogeny.taxa()

# set my move index
mvi = 0
mni = 0

H = 0.587405

####################
# Create the rates #
####################

### Specify a prior on the diversification and turnover rate

### Create the constant prior parameters of the diversification rates
## Number of surviving lineages is 103
rate_mean <- ln( ln(103/2.0) / observed_phylogeny.rootAge() )
rate_sd <- 2*H

for (i in 1:NUM_STATES) {

### Create a lognormal distributed variable for the diversification rate
log_speciation[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
speciation[i] := exp( log_speciation[i] )
moves[++mvi] = mvSlide(log_speciation[i],delta=0.20,tune=true,weight=3.0)
#moves[++mvi] = mvScale(log_speciation[i],lambda=0.20,tune=true,weight=3.0)

### Create a lognormal distributed variable for the turnover rate
log_extinction[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
extinction[i] := exp( log_extinction[i] )
moves[++mvi] = mvSlide(log_extinction[i],delta=0.20,tune=true,weight=3)
#moves[++mvi] = mvScale(log_extinction[i],lambda=0.20,tune=true,weight=3.0)
}



#########################################################
# Set up the transition rate matrix for observed states #
#########################################################
## I defined very loosely my gamma priors for rates in the Q-mat
shape_pr := 0.5
#rate_pr := 1
############### Alternative definition or rate parameter
# Each transition rate between observed states are drawn
# from an exponential distribution with a mean of 10
# character state transitions over the tree.
 rate_pr := observed_phylogeny.treeLength()/10

# Diploid hermaphroditic 0
# Diploid Unisexual 1
# Polyploid Hermaphroditic 2
# Polyploid Unisexual 3

# No reversal for sexual system or polyploidy
rate_01 ~ dnGamma(shape=shape_pr, rate=rate_pr)
rate_10 := 0
rate_23 ~ dnGamma(shape=shape_pr, rate=rate_pr)
rate_32 := 0
rate_02 ~ dnGamma(shape=shape_pr, rate=rate_pr)
rate_20 :=0 
rate_13 ~ dnGamma(shape=shape_pr, rate=rate_pr)
rate_31 := 0

moves[++mvi] = mvScale( rate_01, weight=2 )
moves[++mvi] = mvScale( rate_23, weight=2 )
moves[++mvi] = mvScale( rate_02, weight=2 )
moves[++mvi] = mvScale( rate_13, weight=2 )


for (i in 1:4) {
for (j in 1:4) {
q[i][j] := 0.0
}
}
q[1][2] := rate_01
q[3][4] := rate_23
q[1][3] := rate_02
q[2][4] := rate_13


# generate the actual matrix
rate_matrix := fnFreeK(q, rescaled=false, matrixExponentialMethod="scalingAndSquaring")

#####################################
# Set up the root state frequencies #
# We're going to estimate them and not set them up#
#####################################

### Create a constant variable with the prior probabilities of each rate category at the root.
rate_category_prior ~ dnDirichlet( rep(1,NUM_STATES) )
moves[++mvi] = mvDirichletSimplex(rate_category_prior,tune=true,weight=2)


### Create the fix parameter for the age of the root set to the observed age
root <- observed_phylogeny.rootAge()

### I did not add any sampling of species but it is posible by setting this rho parameter
### rho is the probability of sampling species at the present
### fix this
rho <- 0.5


####################################################################
# Building the BiSSE Model as discrete character model+ BD process#
###################################################################

### Here is where I tie speciation, extinction, and Q using a Birth-Death with categories
timetree ~ dnCDBDP( rootAge           = root,
speciationRates   = speciation,
extinctionRates   = extinction,
Q                 = rate_matrix,
pi                = rate_category_prior,
rho               = rho,
delta             = 1.0,
condition         = "time" )

### clamp the model with the "observed" tree
timetree.clamp( observed_phylogeny )
timetree.clampCharData( data )



#############
# The Model #
#############


### workspace model wrapper ###
mymodel = model(rate_matrix)

### set up the monitors that will output parameter values to file and screen
#monitors[++mni] = mnModel(filename="output/bissepollss.log", printgen=1)
monitors[++mni] = mnModel(filename="output/musse_ploidyss.log", printgen=1)
monitors[++mni] = mnJointConditionalAncestralState(tree=timetree, cdbdp=timetree, type="NaturalNumbers", printgen=1000, withTips=true, withStartStates=false, filename="output/anc_states_musse_ploidyss.log")
monitors[++mni] = mnScreen(printgen=10, rate_01, rate_02, speciation, extinction)


################
# The Analysis #
################

### workspace mcmc
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, moveschedule="random")

### pre-burnin to tune the proposals 20% of the sample
#mymcmc.burnin(generations=2000,tuningInterval=100)

### run the MCMC
mymcmc.run(generations=200000)

##############################
# Summarize ancestral states #
##############################


#anc_states = readAncestralStateTrace("basicdata/anc_states_musse_ploidyss.log")
#anc_tree = ancestralStateTree(tree=observed_phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file="basidata/anc_states_ploidyss.tree", burnin=1000, summary_statistic="MAP", site=0)

q()


